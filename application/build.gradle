plugins {
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.apache.pdfbox:pdfbox:2.0.26'
    implementation 'jakarta.activation:jakarta.activation-api:2.1.0'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.0'
    implementation 'org.javassist:javassist:3.29.0-GA'
    implementation 'org.freemarker:freemarker:2.3.31'
    implementation 'com.thoughtworks.xstream:xstream:1.4.20'
    implementation 'com.google.flogger:flogger:0.7.4'
    implementation 'com.google.flogger:flogger-system-backend:0.7.4'
    implementation 'org.springframework.boot:spring-boot-starter'

    // detects system dark / light setting
    // https://github.com/Dansoftowner/jSystemThemeDetector
    implementation 'com.github.Dansoftowner:jSystemThemeDetector:3.6'

    // Theming
    // https://www.formdev.com/flatlaf/
    implementation 'com.formdev:flatlaf:3.2'

    // https://mvnrepository.com/artifact/org.freemarker/freemarker
    implementation 'org.freemarker:freemarker:2.3.31'

    // Database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.14'
    implementation 'org.liquibase:liquibase-core:4.17.2'
    implementation 'org.liquibase.ext:liquibase-hibernate5:4.22.0'
    runtimeOnly 'org.postgresql:postgresql:42.5.1'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    //
    // Test Dependencies
    //

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Spring Boot Starter Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.14'

    // Database
    // https://mvnrepository.com/artifact/com.h2database/h2
    testImplementation 'com.h2database:h2:2.2.224'

    // https://mvnrepository.com/artifact/org.easymock/easymock
    testImplementation 'org.easymock:easymock:5.2.0'

    // Approval Tests
    testImplementation 'com.approvaltests:approvaltests:18.6.0'
}

group = 'de.heinerion'
version = '1.0.0'
description = 'InvoiceManager'
java.sourceCompatibility = JavaVersion.VERSION_16

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

configurations {
    liquibase
}

// some problems with hibernate envers: https://github.com/liquibase/liquibase-hibernate/issues/260
tasks.register('liquibaseDiffChangeLog', JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"

    args "--defaultsFile=src/main/resources/liquibase-cli.properties"
    args "--changeLogFile=src/main/resources/db/changelog/${buildTimestamp()}.yaml"
    args "diffChangeLog"
}

tasks.register('liquibaseinit', JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"

    args "--defaultsFile=src/main/resources/liquibase-cli.properties"
    args "initProject"
}

static def buildTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HH-mm-ss')
    return formattedDate
}

tasks.named("bootJar") {
    mainClass = 'de.heinerion.App'
}

test {
    useJUnitPlatform()
    testLogging { exceptionFormat = 'full' }
    systemProperty 'spring.profiles.active', 'test'
}